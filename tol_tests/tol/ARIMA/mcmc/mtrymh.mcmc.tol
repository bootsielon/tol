@MTryMetropolis_ARIMA mtm_arima = @MTryMetropolis_ARIMA::New(
  Z,
  SIGMA,
  arima.0,
  False);

Real mtm_arima::_.k := If(ObjectExist("Real","k"), k, 
  IntRand(1,mtm_arima::_.n));

Matrix mcmc = Rand(0,mtm_arima::_.n,0,0);

Matrix L = {
  Matrix COV = modEst.0::Correlations::COV;
  Real n = mtm_arima::_.n;
  Real r = Rows(COV);
  Real k = r-n+1;
  Choleski(Sub(COV,k,k,n,n))
};
Matrix Li = LTInverse(L);
VMatrix mtm_arima::_.L := Mat2VMat(L);
VMatrix mtm_arima::_.Li := Mat2VMat(Li);
Real mtm_arima::_.s := 1;


Real numSim = 0;

Real append_draw(VMatrix draw)
{
  If(!(numSim%100), WriteLn("
##############################################################################
Simulation "<<numSim+ " _.acceptRatio="<<mtm_arima::_.acceptRatio));
  Matrix mcmc := mcmc << VMat2Mat(Tra(draw));
  Real numSim := numSim+1
};

/* * / 
Real mtm_arima::_.sFactor := 1.01;
Real numSim := 0;
Real mh.t0 = Copy(Time);
While(numSim<1000, append_draw(mtm_arima::draw_single_try(0)));
Real mh.time = (Copy(Time) - mh.t0)/numSim;
WriteLn("Time by MH simulation "<<mh.time);

/* */
Real mtm_arima::_.sFactor := 1.00;
Real numSim := 0;
Real mtm.t0 = Copy(Time);
//Real TolOprProfiler.Enabled := True;
While(And(numSim<2500,NError==numErr.0), append_draw(mtm_arima::draw_multiple_try(0)));
//Real TolOprProfiler.Dump("draw_multiple_try.k"<<mtm_arima::_.k+".csv");
Real mtm.time = (Copy(Time) - mtm.t0)/numSim;
Real mtm.speed = mtm_arima::_.acceptRatio / mtm.time;
WriteLn("Time by MTM simulation "<<mtm.time);
/* */
Real numErr = Copy(NError) - numErr.0;
Real numWar = Copy(NWarning) - numWar.0;

Text report.path = "mtrymh.tab";

Real raftery.burnin = ?;
Real raftery.thinning = ?;
Real raftery.rate = ?;
Real raftery.speed = ?;

Matrix cmp = mcmc | Group("ConcatColumns", For(1,Rows(PARAM),Matrix(Real j) 
{
  Constant(Rows(mcmc),1,MatDat(PARAM,j,1))
}));
Set raftery.report = If(numErr, Copy(Empty),
{[[
  Matrix raftery=RCODA::raftery.diag(mcmc,{ [[
    Real verbose=TRUE, 
    Real varByCol=TRUE,
    Real q = 0.025;
    Real r = 0.007;  
    Real s = 0.950;  
    Real eps = 0.001
  ]]});
  Real raftery.burnin := MatMax(SubCol(raftery,[[1]]));
  Real raftery.thinning := MatMax(SubCol(raftery,[[4]]));
  Real raftery.rate := (numSim-raftery.burnin)/(numSim*raftery.thinning);
  Real raftery.speed := raftery.rate / mtm.time
]]});
  
Text report = ""
  <<rndSeed+"\t"
  <<rndSeed_iv+"\t"
  <<arima.label+"\t"
  <<mtm_arima::_.n+"\t"
  <<mtm_arima::_.k+"\t"
  <<arima.time+"\t"
  <<mtm.time+"\t"
  <<mtm_arima::_.acceptRatio+"\t"
  <<mtm.speed+"\t"
  <<raftery.burnin+"\t"
  <<raftery.thinning+"\t"
  <<raftery.rate+"\t"
  <<raftery.speed+"\t"
  <<numErr+"\t"
  <<numWar+"\n";
Text If(!FileExist(report.path), WriteFile(report.path,""
  +"rndSeed\t"
  +"rndSeed_iv\t"
  +"arima.label\t"
  +"mtm_arima::_.n\t"
  +"mtm_arima::_.k\t"
  +"arima.time\t"
  +"mtm.time\t"
  +"mtm_arima::_.acceptRatio\t"
  +"mtm.speed\t"
  +"raftery.burnin\t"
  +"raftery.thinning\t"
  +"raftery.rate\t"
  +"raftery.speed\t"
  +"numErr\t"
  +"numWar\n"));

WriteLn(report);
Text AppendFile(report.path,report);


