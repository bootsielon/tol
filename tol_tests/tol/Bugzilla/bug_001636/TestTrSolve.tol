Real PutRandomSeed(1000);
Matrix A = Rand(3, 6, 0, 1);
Matrix L = Choleski(A*Tra(A));

Real nSol = 4;

Matrix Br = Rand(Rows(L), nSol, 0, 1);
Matrix Bc = Tra(Br);

Set checkAll = Copy(Empty);

Real tolTest = 1.0e-10;

Real AppendCheck(Matrix Bhat, Matrix B, Text name)
{
  Real check0 = MatFrobeniusNorm(Bhat - B);
  Real check = If(IsUnknown(check0),0,check0<tolTest);
  Set Append(checkAll, [[PutName(name, check)]]);
  Real 1
};

// Matrix TrSolve (Matrix T, Matrix B [, Real left=1, Real trans=0, Real lower=1])
// L*X=B
Matrix X_LXB = TrSolve(L, Br, 1, 0, 1);
Real AppendCheck(L*X_LXB, Br, "LXB");

// L'*X=B
Matrix X_LtXB = TrSolve(L, Br, 1, 1, 1);
Real AppendCheck(Tra(L)*X_LtXB, Br, "LtXB");

// X*L=B
Matrix X_XLB = TrSolve(L, Bc, 0, 0, 1);
Real AppendCheck(X_XLB*L, Bc, "XLB");

// X*L'=B
Matrix X_XLtB = TrSolve(L, Bc, 0, 1, 1);
Real AppendCheck(X_XLtB*Tra(L), Bc, "XLtB");

Matrix U = Tra(L);

// U*X=B
Matrix X_UXB = TrSolve(U, Br, 1, 0, 0);
Real AppendCheck(U*X_UXB, Br, "UXB");

// U'*X=B
Matrix X_UtXB = TrSolve(U, Br, 1, 1, 0);
Real AppendCheck(Tra(U)*X_UtXB, Br, "UtXB");

// X*U=B
Matrix X_XUB = TrSolve(U, Bc, 0, 0, 0);
Real AppendCheck(X_XUB*U, Bc, "XUB");

// X*U'=B
Matrix X_XUtB = TrSolve(U, Bc, 0, 1, 0);
Real AppendCheck(X_XUtB*Tra(U), Bc, "XUtB");

Set View(checkAll, "");
