//////////////////////////////////////////////////////////////////////////////
//FILE: test.tol
//AUTHOR: vdebuen@tol-project.org
//CREATION: 2008-10-14
//PURPOSE: Chequea Bsr-Hlm con filtro no lineal del input de tipo Ad-Stock.
//////////////////////////////////////////////////////////////////////////////

Text email_ = "vdebuen@tol-project.org"; 
Text link_ = "";
Text summary_ = 
"Chequea Bsr-Hlm con filtro no lineal del input de tipo Ad-Stock." ;

Real numErr0 = Copy(NError);

//////////////////////////////////////////////////////////////////////////////
//Loads general tools for BSR tests
//////////////////////////////////////////////////////////////////////////////
Set Include("../general/general.tol");
Set Include("../general/NonLinearFilter/_PubAdStock_ExpBetaDelta.tol");

//Random seed setting
Real PutRandomSeed(0);
Real rndSeed = GetRandomSeed(0);
WriteLn("Current Random Seed = "<<rndSeed);

//////////////////////////////////////////////////////////////////////////////
//NOTA IMPORTANTE: Los comentarios dentro de un NameBlock precedidos de ## 
//significan que el método o miembro a continuación es obligatorio para 
//ser usado por la API BsrHLM, debe estar presente con el mismo nombre, tipo y 
//estructura interna. Por supuesto el contenido sí puede cambiar para 
//adaptarse al caso del modelo analizado.
//Los precedidos de un sólo # son recomendables pero no obligatorios y se 
//puede cambiar el nombre sin problemas pero respetando la acción si es 
//posible.
//Los miembros y métodos de un NameBlock que empiezan con _ son privados y 
//no se pueden acceder desde fuera, a noser que empiecen por _. en cuyo caso
//son de sólo lectura, es decir, se pueden consultar pero no modificar desde
//fuera del NameBlock
//////////////////////////////////////////////////////////////////////////////

Real BysMcmc::Bsr::DynHlm::DBApi::doQueryTrace := False;


//Se activa la base de datos de modelos BsrHlm
Real DBConnection::BsrHlm::Activate(0);

//////////////////////////////////////////////////////////////////////////////
WriteLn("Creando en la base de datos el área documental");
//////////////////////////////////////////////////////////////////////////////

//Identificador del objetivo modelo.
//Debería distinguir de forma inequívocamente el proyecto, el concepto que
//se quiere analizar y otros detalles como el fechado o intervalo al que se 
//refiere.
//Se recomienda usar el mismo nombre del fichero y dejarlo fijo.
Text id_model =  "Tdt.Test.AdStock.In";

//Descripción del objetivo modelo
//Sin extenderse demasiado debe dejar muy claro l oque pretende el modelo
Text ds_model = "Modelo generado automáticamente por simulación para el "
"chequeo de la implementación de la simulación de funciones de filtro no "
"lineal del input de tipo PubAdStock_ExpBetaDelta";

//Identificador de la sesión o implementación
//Es recomendable cambiarla cada vez que se produzca un cambio no trivial en 
//la definición y que siga algún tipo de orden, cronológico, alfabético u
//ordinal que permita a simple vista saber cual es anterior a otra.
//Si una sesión va asociada a un único modelo es prudente que el nombre lo 
//recoja para evitar confusiones. En este caso se infija con A3Aud7CH como
//abreviatura del nombre del modelo.
Text id_session =  "SES_Tdt.Test.AdStock.In.01";

//Esta orden desbloquea todas las sesiones relativas a este modelo
Real BysMcmc::Bsr::DynHlm::DBApi::Mod.SetBlocked(
  DBConnection::BsrHlm::_.defaultDB, id_model, 0);
//Esta orden borra todas las sesiones relativas a este modelo
Real BysMcmc::Bsr::DynHlm::DBApi::Mod.Delete(
  DBConnection::BsrHlm::_.defaultDB, id_model);


//Descripción de la sesión o implementación
//Se recomienda introducir un comentario sobre los cambios introducidos con
//respecto a la última sesión
Text ds_session =  "Modelo de un año en diario con función de transferencia "
" omega_1/(1-delta_1*B) y un input de nivel";

//Autores
//Deben firmar al menos los que han hecho los cambios de la presente sesión,
//preferentemente con su email separados por ;
Text ds_authors  = 
  "vdebuen@tol-project.org;";


Real BysMcmc::Bsr::DynHlm::DBApi::Mod.Create(
  DBConnection::BsrHlm::_.defaultDB, id_model,   ds_model);
Real BysMcmc::Bsr::DynHlm::DBApi::Ses.Create(
  DBConnection::BsrHlm::_.defaultDB, id_session, ds_session, ds_authors);
Real BysMcmc::Bsr::DynHlm::DBApi::ModSes.Create(
  DBConnection::BsrHlm::_.defaultDB, id_model, id_session);

//////////////////////////////////////////////////////////////////////////////
WriteLn("Creando en la base de datos los niveles de la jerarquía");
//////////////////////////////////////////////////////////////////////////////
Real {BysMcmc::Bsr::DynHlm::DBApi::ModSes.SetLevels(
  DBConnection::BsrHlm::_.defaultDB, id_model, id_session,
  Real maxLatLevel = 0,       //Máximo nivel de latencia
  Real hasPrior    = False,   //Indica si hay nodo a priori
  Real hasMixture  = False)}; //Indica si hay restricciones que afectan a
                              //variables de distintos nodos

//////////////////////////////////////////////////////////////////////////////
WriteLn("Creando en la base de datos los nodos observacionales");
//////////////////////////////////////////////////////////////////////////////


//////////////////////////////////////////////////////////////////////////////
//Crea el manejador de series de un nodo observacional concreto
//En este caso sólo hay un nodo observacional 
  NameBlock node.series.handler(Real nu_node)
//////////////////////////////////////////////////////////////////////////////
{[[
//Real nu_node = 1;
  Text te_node = FormatReal(nu_node,"%04.0lf");
  //##Nombre del nodo
  Text _.id_node = "Obs.Node_"+te_node;
  //##Descripción del nodo
  Text _.description = "Nodo observacional número "+te_node;

  //Para recorte de series input y output
  NameBlock tmIn = GetTimeInterval(y2008m01d01, y2008m12d31);

  Real _priMesAdStock.delta  = Rand(0.60,0.99);
  Real _priMesAdStock.iniVal = 
  {
    Serie aux0 = tmIn::sub.move(CalInd(D(1),C),0,-10000,0);
    Ratio rat  = (1-_priMesAdStock.delta)/(1-_priMesAdStock.delta*B);
    Serie aux1 = DifEq(rat, aux0);
    FirstS(tmIn::sub.move(aux1,0,-1,0))
  };
  Set _.nonLinearFilters = {[[
    //Filtro no lineal de tipo Ad-Stock
    NameBlock priMesAdStock = PubAdStock_ExpBetaDelta(
      Real  enabled = True,
      Text  segmentName = _.id_node,
      Text  inputName = "priMesAdStock",
      Serie oriInp = tmIn::sub(CalInd(D(1),C),0),
      Set @Bsr.Param.Info("Omega", Rand(500,1000),100,5000),
      Set @Bsr.Param.Info("Beta",  Rand(0.60,0.60),0.0001, 1),
      Set @Bsr.Param.Info("Delta", _priMesAdStock.delta,0.0001,0.9999),
      Set @Bsr.Param.Info("IniVal",_priMesAdStock.iniVal,
                                _priMesAdStock.iniVal,_priMesAdStock.iniVal),
      Real simulate.iniVal=False
    ) ]]};
  //#Se comprueba la unicidad de nombres y se indexa para acelerar 
  //búsquedas
  Real _checkUniqueNonLinFlt = SetIndexByName(_.nonLinearFilters);
 
  NameBlock real.data = {Generator::ObsNode(
    tmIn, 
    Real sigma = Rand(0.8,1.2), 
    Set arima = Copy(Empty), 
    Set linInput =
    [[
      NameBlock constante = Generator::Input(tmIn,5,10,CalInd(C,C));
      NameBlock lunes = Generator::Input(tmIn,10,15,CalInd(WD(1),C))
    ]],
    Set transFunDelta = Copy(Empty),
    Set nonLinFilInp = 
    [[
      NameBlock priMesAdStock = Generator::NonLinFltInp(
        tmIn, 500, 1000, _.nonLinearFilters::priMesAdStock)
    ]],
    Set nonLinFilOut = Copy(Empty))};
  
  //#Contiene todas las series input y output con nombres únicos dentro
  //de cada nodo.
    Set _.series = 
    { [[ Serie output = real.data::_.output ]]} << real.data::_.input;
  //#Se comprueba la unicidad de nombres y se indexa para acelerar 
  //búsquedas
  Real _checkUniqueSeries = SetIndexByName(_.series);

  Real disturbInitValuesForNonLinFilters =
  {
    NameBlock gen = (real.data::_.nonLinFilInp)[1];
    Matrix param = gen::RandNonLinParam(0);
  //Real _.nonLinearFilters::priMesAdStock::set.parameter(param);
    1
  };

  //##Devuelve una serie del nodo dado un identificador único
  Serie GetSerie(Text id_serie)
  {
    _.series[id_serie]
  };
  //##Devuelve un filtro no lineal del nodo dado un identificador único
  NameBlock GetNonLinFlt(Text id_nonLinFlt)
  {
    _.nonLinearFilters[id_nonLinFlt]
  };

  //##Información adicional del modelo sobre el output
  NameBlock _.output = 
  {[[
    //##Nombre con el que reconoce la serie en la base de datos
    Text _.name = "output";
    //##Serie output
    Serie _.serie = _.series::output;
    //##Número de sigmas para la distribución a priori de los omitidos
    //se multiplicará por la desviación típica muestral de la serie.
    //La media a priori será la de la propia serie.
    Real _.mis_pri_sig_fac = 3;
    //##Valor mínimo a priori para los omitidos
    Real _.mis_pri_min     = -1/0;
    //##Valor máximo a priori para los omitidos
    Real _.mis_pri_max     = +1/0;
    //##Fecha inicial de estimación del nodo
    Date _.firstDate = tmIn::_.firstDate;
    //##Fecha final de estimación del nodo
    Date _.lastDate = tmIn::_.lastDate;
    //##Parte ARIMA asociada
    Set _.arima = Copy(Empty)
  ]]};

  //#Returns true if differenced serie is not null
  Real notNullDiff(Serie ser)
  {
    Polyn _difference = ARIMAGetDIF(_.output::_.arima);
    Serie difSer = _difference:ser;
    Not(And(Eq(MaxS(difSer),0), Eq(MinS(difSer),0)))
  };

  Real _.useRealDataAsInitValue = False;
  //##Información adicional del modelo sobre los inputs
  //Cada elemento es un NameBlock cuyo nombre será el identificador en
  //la base de datos de BsrHlm y es obligatorio que se llame igual que
  //la correspondiente serie input
  Set _.input = { _.nonLinearFilters << SetOfNameBlock( 
  NameBlock constante = [[
    //##Serie input
    //Los días de fiesta la gente suele salir de casa más y ver menos tele
    Serie _.serie = _.series::constante;
    //##Si es falso no se usará este input
    Real _.enabled = notNullDiff(_.serie);
    //##Número de sigmas para la distribución a priori de los omitidos
    Real _.mis_pri_sig_fac =  5;
    //##Valor mínimo a priori para los omitidos
    Real _.mis_pri_min     =  -1/0;
    //##Valor máximo a priori para los omitidos
    Real _.mis_pri_max     =  +1/0;
    //##Valor inicial del parámetro
    Real _.initValue       =  If(!_.useRealDataAsInitValue,0,
                                 real.data::_.effects::constante::_.coef);
    //##Valor mínimo a priori del parámetro
    Real _.minimum         = -1/0;
    //##Valor máximo a priori del parámetro
    Real _.maximum         = +1/0
  ]], 
  NameBlock lunes = [[
    //##Serie input
    //Los días de fiesta la gente suele salir de casa más y ver menos tele
    Serie _.serie = _.series::lunes;
    //##Si es falso no se usará este input
    Real _.enabled =notNullDiff(_.serie);
    //##Número de sigmas para la distribución a priori de los omitidos
    Real _.mis_pri_sig_fac =  5;
    //##Valor mínimo a priori para los omitidos
    Real _.mis_pri_min     =  -1/0;
    //##Valor máximo a priori para los omitidos
    Real _.mis_pri_max     =  +1/0;
    //##Valor inicial del parámetro
    Real _.initValue       =  If(!_.useRealDataAsInitValue,0,
                                 real.data::_.effects::lunes::_.coef);
    //##Valor mínimo a priori del parámetro
    Real _.minimum         = -1/0;
    //##Valor máximo a priori del parámetro
    Real _.maximum         = +1/0
  ]] )};

  //Se comprueba de nuevo la unicidad de los manejadores de los input
  Real _checkUniqueInput = SetIndexByName(_.input)

]]};

//////////////////////////////////////////////////////////////////////////////
//Manejador de series del modelo
  NameBlock series.handler=BysMcmc::Bsr::DynHlm::DBApi::CreateSeriesHandler( 
//////////////////////////////////////////////////////////////////////////////
For(1, 1, NameBlock(Real ch)
{ 
  WriteLn("[series.handler] Creando manejador de series del nodo "+
          IntText(ch));
  NameBlock aux = node.series.handler(ch);
  Eval(aux::_.id_node+"=aux")
})); 

//////////////////////////////////////////////////////////////////////////////
WriteLn("Creando en la base de datos los nodos observacionales");
//////////////////////////////////////////////////////////////////////////////
Real BysMcmc::Bsr::DynHlm::DBApi::ModSes.Obs.CreateAll(
  DBConnection::BsrHlm::_.defaultDB, id_model, id_session,
  series.handler::_.obs.node);

/* */

//////////////////////////////////////////////////////////////////////////////
//Estimación del modelo con el sistema Bsr::DynHlm
//////////////////////////////////////////////////////////////////////////////
BysMcmc::@Config bsr.config = [[
//MCMC dimensions
  Real mcmc.burnin        =    500; 
  Real mcmc.sampleLength  =   2200;
  Real mcmc.cacheLength   =    100;

  Text DBApi.modularSchema = 
    BysMcmc::Options::DBApi.Modular.Schema::Master.ObsAsPrimary;
//  BysMcmc::Options::DBApi.Modular.Schema::Joint.All;
//Generic flags
  Real do.resume          =  0;
  Real do.report          =  True;
  Real do.eval            =  True;
  Real do.linear.effects  =  False
]];


BysMcmc::@Cycler cycler = BysMcmc::Bsr::DynHlm::DBApi::GetCycler
(
  DBConnection::BsrHlm::_.defaultDB,
  id_model,
  id_session,
  Ois.DefRoot+"HlmBsrTest/",
  series.handler,
  bsr.config
);
//Real cycler::initialize(0);
BysMcmc::@Estimation bsr.estim = BysMcmc::@Estimation::New(cycler);
//Real TolOprProfiler.Enabled := True;
Real bsr.estim::Run(0);
//Real TolOprProfiler.Dump("ses.01");
Real BysMcmc::Bsr::DynHlm::DBApi::CloseModel(bsr.estim);

//////////////////////////////////////////////////////////////////////////////
Set compare_forecast = 
//////////////////////////////////////////////////////////////////////////////
For(1,Card(series.handler::_.obs.node),Set(Real nu_node)
{
  NameBlock node = (series.handler::_.obs.node)[nu_node];
  NameBlock known = node::real.data;
  Set estim = (bsr.estim::eval.average)[nu_node];
  Set aux = [[
    //Compara los valores de los parámetros reales y estimados
    Matrix param.cmp = known::_.param | Tra(bsr.estim::param.average);
    //Filtro real del input
    Serie FilterOut = known::_.filterOut, 
    //Filtro estimado del output
    Serie FilterOut.Estim = estim::serOutput.extFil;
    //Filtro real del input
    Serie FilterInp = known::_.filterInp, 
    //Filtro estimado del input
    Serie FilterInp.Estim = estim::serFilter.full;
    //Filtro real
    Serie Filter = FilterOut + FilterInp, 
    //Filtro estimado
    Serie Filter.Estim = FilterOut.Estim + FilterInp.Estim ;
    //Noise real
    Serie Noise = known::_.noise, 
    //Noise estimado
    Serie Noise.Estim = estim::serNoise;
    //Output real
    Serie Output = known::_.output, 
    //Previsión
    Serie Forecast = estim::serForecast;
    //Error en términos absolutos
    Serie Error = Output-Forecast;
    //Error relativo medio
    Real Mape = AvrS(Abs((Output - Forecast)/Output))
  ]];
  Eval(node::_.id_node+"=aux")
});


/* */ 
Real DBConnection::BsrHlm::Close(0);
/* */ 

Real numErr1 = Copy(NError);

//Extented information about test behaviour
Set partialResults_ = 
[[
  numErr0, numErr1
]];

//This is a messure of the success of the test 
Real quality_ = And(numErr0 == numErr1)*  
{
  Serie res_=compare_forecast::Obs.Node_0001::Error; 
  Serie res =series.handler::_.obs.node::Obs.Node_0001::real.data::_.noise;
  Min(1,StDsS(res)/StDsS(res_))
};

//Return the results 
Set resultStr_ = @strTestStatus(summary_, link_, quality_,
                  "Partial results = "<<partialResults_,
                  "NO DBServerType", "NO DBServerHost", "NO DBAlias",
                  email_);
WriteLn(""<<resultStr_);
resultStr_
/* */ 

