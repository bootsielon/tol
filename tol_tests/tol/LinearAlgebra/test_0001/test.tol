//////////////////////////////////////////////////////////////////////////////
// FILE   : test.tol
// PURPOSE: test for Matrix Algebra Coherence
//////////////////////////////////////////////////////////////////////////////

  Text email_ = "vdebuen@tol-project.org"; 
  Text link_ = "";
  Text summary_ = "Matrix arithmetic" ;

//Write here the initial test TOL code if needed
  Real numErr0 = Copy(NError);
  Real r = 3;
  Real c = 5;
  Matrix A_ = Rand(r,c,-10,10);
  Matrix B_ = Rand(r,c,-10,10);
  Real   x  = Rand(-10,10);
//Internal Sum 
  Matrix T11 = A_+B_;
  Matrix T12 = Tra(Tra(B_)+Tra(A_));
//Sum with real
  Matrix T21 = A_+x;
  Matrix T22 = Tra(Tra(A_)+x);
//Rest
  Matrix T31 = A_-B_;
  Matrix T32 = -Tra(Tra(B_)-Tra(A_));
//Rest with real
  Matrix T41 = A_-x;
  Matrix T42 = Tra(Tra(A_)-x);
//Matrix product
  Matrix T51 = Tra(A_)*B_;
  Matrix T52 = Tra(Tra(B_)*A_);
//Product with real
  Matrix T61 = A_*x;
  Matrix T62 = Tra(Tra(A_)*x);
//Cell by cell product
  Matrix T71 = A_ $* B_; 
  Matrix T72 = Tra(Tra(B_)$*Tra(A_));
//Cell by cell quotient
  Matrix T81 = A_ $/ B_; 
  Matrix T82 = Tra(Tra(A_)$/Tra(B_));
//Power to real
  Matrix T91 = Abs(A_)^x;
  Matrix T92 = Tra(Tra(Abs(A_))^x);

  Set difAll = [[
    Real diff1 = MatMax(Abs(T11-T12)),
    Real diff2 = MatMax(Abs(T21-T22)),
    Real diff3 = MatMax(Abs(T31-T32)),
    Real diff4 = MatMax(Abs(T41-T42)),
    Real diff5 = MatMax(Abs(T51-T52)),
    Real diff6 = MatMax(Abs(T61-T62)),
    Real diff7 = MatMax(Abs(T71-T72)),
    Real diff8 = MatMax(Abs(T81-T82)),
    Real diff9 = MatMax(Abs(T91-T92))
  ]];
  Real difMax = SetMax(difAll);
  Real q = 1-difMax;
//Extented information about test behaviour
  Set partialResults_ = difAll <<
  [[
    difMax, q
  ]];

//This is a messure of the success of the test 
  Real quality_ = And(numErr0 == NError)*q;

//Return the results 
  Set resultStr_ = strTestStatus(summary_, link_, quality_,
                    "Partial results = "<<partialResults_,
                    "NO DBServerType", "NO DBServerHost", "NO DBAlias",
                    email_);
  WriteLn(""<<resultStr_);
  resultStr_

