/////////////////////////////////////////////////////////////////////////////
// LIBRARY: StdtLib (Standard Library of TOL)
// FILE: _estim.tol
// PURPOSE: 
/////////////////////////////////////////////////////////////////////////////

//////////////////////////////////////////////////////////////////////////////
Class Estimation
//////////////////////////////////////////////////////////////////////////////
{
  BysMcmc::Cycler cycler;
  Set report = Copy(Empty);
  Matrix param.average = Rand(0,0,0,0);
  Set eval.average = Copy(Empty);
  Set linear.effects = Copy(Empty)
};

//////////////////////////////////////////////////////////////////////////////
Text _.autodoc.member.Estim =
"Estimates a bayesian model\n";
Estimation Estim(Cycler cycler_)
//////////////////////////////////////////////////////////////////////////////
{
  BysMcmc::Estimation estim = 
  [[
    BysMcmc::Cycler cycler = cycler_
  ]];                                
  BysMcmc::Config config = cycler_::_.config;
  BysMcmc::Notifier notifier = cycler_::_.notifier;
  Real StoreRecover(cycler_, config::partialSampling.Sequential);
  Real cycler_::initialize(0);
  Real sampleLength = config::mcmc.burnin+config::mcmc.sampleLength;

  
  Real If(config::do.mcmc.profiler, 
    TolOprProfiler.Enabled := True);
  Real cycler_::generate(sampleLength); 
  Real If(config::do.mcmc.profiler, 
    TolOprProfiler.Dump(cycler::_.path+cycler::_.name));

  Real notifier::reporting(0);
  Real If(config::do.report,
  {
    Set estim::report := cycler_::report(config::mcmc.burnin,
                                         config::mcmc.sampleLength,
                                         config::mcmc.thinning);
    Matrix estim::param.average := 
    {
      Tra(SetMat(Extract(estim::report::coda.summary, 2)))
    };
    Real If(config::do.eval,
    {
      Real notifier::evaluating(0);
      Set estim::eval.average := cycler_::_.sampler::eval(estim::param.average);
      1
    });
    1 
  });
  estim
};
