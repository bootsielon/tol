//////////////////////////////////////////////////////////////////////////////
// FILE   : _masterSampler.tol
// PURPOSE: Declares class BysMcmc::MasterSampler
// PURPOSE: Generic handler for masters in MonteCarlo Markov Chain methods
//////////////////////////////////////////////////////////////////////////////

//////////////////////////////////////////////////////////////////////////////
Class MasterSampler
//////////////////////////////////////////////////////////////////////////////
{
  //Stores all blocks in expected order
  Set _.blocks;
  //User configuration parameters
  BysMcmc::Config _.config;
  //Messages identifier
  Text _MID;

  Real _.N = ?;
  Real _.fixedParamNum = ?;
  Real _.reloadParamNum = ?;
  Real _.freeParamNum = ?;

  //Full MCMC partial sampling handler
  BysMcmc::McmcPartialHandler _.mcmcPartialHandler;

  Set  eval(Matrix values);
  Text get.name(Real unused);
  Text get.session(Real unused);
  Text get.path(Real unused);

  ////////////////////////////////////////////////////////////////////////////
  Set get.colNames(Real unused) 
  ////////////////////////////////////////////////////////////////////////////
  { 
    Set gcn(BysMcmc::BlockSampler blk)
    {
      blk::_.colNames
    };
    BinGroup("<<",EvalSet(_.blocks, gcn))
  };

  ////////////////////////////////////////////////////////////////////////////
  Set get.fixedParam(Real unused) 
  ////////////////////////////////////////////////////////////////////////////
  { 
    Set gfp(BysMcmc::BlockSampler blk)
    {
      blk::_.partialSampler::_.fixedParam
    };
    BinGroup("<<",EvalSet(_.blocks, gfp))
  };

  ////////////////////////////////////////////////////////////////////////////
  Set get.reloadParam(Real unused) 
  ////////////////////////////////////////////////////////////////////////////
  { 
    Set grp(BysMcmc::BlockSampler blk)
    {
      blk::_.partialSampler::_.reloadParam
    };
    BinGroup("<<",EvalSet(_.blocks, grp))
  };


  ////////////////////////////////////////////////////////////////////////////
  Text _.autodoc.member.define =
  "";
  Real define(Real unused)
  ////////////////////////////////////////////////////////////////////////////
  {
    Real _.N := 0; 
    Real _.fixedParamNum := 0;
    Real _.reloadParamNum := 0;
    Real count.blk(BysMcmc::BlockSampler blk)
    {
      Real _.N := _.N + blk::_.N; 
      Real _.fixedParamNum := _.fixedParamNum + 
                              Card(blk::_.partialSampler::_.fixedParam); 
      Real _.reloadParamNum := _.reloadParamNum + 
                              Card(blk::_.partialSampler::_.reloadParam);
      1
    };
    Set EvalSet(_.blocks, count.blk);
    Real _.freeParamNum := _.N - (_.fixedParamNum + _.reloadParamNum);
    1
  };

  ////////////////////////////////////////////////////////////////////////////
  Text _.autodoc.member.initialize.blocks =
  "Applies initialize method for each block";
  Real initialize.blocks(Real unused)
  ////////////////////////////////////////////////////////////////////////////
  {
    Real _.N := 0; 
    Real _.fixedParamNum := 0;
    Real _.reloadParamNum := 0;
    Real init.blk(BysMcmc::BlockSampler blk)
    {
      Real _.N := _.N + blk::_.N; 
      Real _.fixedParamNum := _.fixedParamNum + 
                              Card(blk::_.partialSampler::_.fixedParam); 
      Real _.reloadParamNum := _.reloadParamNum + 
                              Card(blk::_.partialSampler::_.reloadParam); 
      Real _.N := _.N + blk::_.N; 
      blk::initialize(0)
    };
    
    SetMin(EvalSet(_.blocks, init.blk))
  };

  ////////////////////////////////////////////////////////////////////////////
  Text _.autodoc.member.draw.blocks =
  "Applies draw method for each block. This is the sampling method for self-"
  "updated blocks.";
  VMatrix draw.blocks(Real numSim)
  ////////////////////////////////////////////////////////////////////////////
  {
    VMatrix draw.blk(BysMcmc::BlockSampler blk)
    {
      blk::get.draw(numSim, False)
    };
    Group("ConcatRows", EvalSet(_.blocks, draw.blk))
  };

  ////////////////////////////////////////////////////////////////////////////
  Text _.autodoc.member.setStore.blocks =
  "Set values of all blocks from a given row vector of values";
  Real setStore.blocks(Matrix values_) 
  ////////////////////////////////////////////////////////////////////////////
  {
    Matrix values = If(Columns(values_)==1,Tra(values_),values_);
  //WriteLn("TRACE "+_.name+"::setStore.blocks("<<Rows(values)+"x"<<Columns(values)+")");   
    Real store.blk(BysMcmc::BlockSampler blk)
    {
      If(Or(!(blk::enabled), blk::_.N<=0),0,
        blk::setStore(blk::getMcmc(values)))
    };
    Set EvalSet(_.blocks, store.blk);
    1
  };

  ////////////////////////////////////////////////////////////////////////////
  Real initialize(Real unused)
  ////////////////////////////////////////////////////////////////////////////
  {
    initialize.blocks(unused)
  };

  ////////////////////////////////////////////////////////////////////////////
  VMatrix draw(Real numSim) 
  ////////////////////////////////////////////////////////////////////////////
  {
    draw.blocks(numSim) 
  };

  ////////////////////////////////////////////////////////////////////////////
  Real setStore(Matrix values) 
  ////////////////////////////////////////////////////////////////////////////
  {
  //WriteLn("TRACE "+_.name+"::setStore("<<Rows(values)+"x"<<Columns(values)+")");   
    setStore.blocks(values) 
  }

};

//////////////////////////////////////////////////////////////////////////////
MasterSampler NoMasterSample(Real unused)
//////////////////////////////////////////////////////////////////////////////
{
  BysMcmc::MasterSampler noMasterSampler = [[
  //Stores all blocks in expected order
  Set _.blocks = Copy(Empty);
  //User configuration parameters
  BysMcmc::Config _.config = BysMcmc::Config.Default(0);
  //Messages identifier
  Text _MID = "";

  Set  eval(Matrix values) { ? };
  Text get.name(Real unused) { ? };
  Text get.session(Real unused) { ? };
  Text get.path(Real unused) { ? };
  Real initialize(Real unused) { ? };
  VMatrix draw(Real numSim) { ? };
  Real setStore(Matrix values) { ? } ]]
};
