/////////////////////////////////////////////////////////////////////////////
// LIBRARY: StdtLib (Standard Library of TOL)
// MODULE: Stat (Statistics)
// FILE: _mainLinearBlock.tol
// PURPOSE: Declares method BysMcmc::Bsr::Gibbs::MainLinearBlock
/////////////////////////////////////////////////////////////////////////////

////////////////////////////////////////////////////////////////////////////
//Method _.buildWorkSpace must create _.ws as an instance of this class
Class WorkSpace.ArimaReg : WorkSpace.LinReg
////////////////////////////////////////////////////////////////////////////
{
  BysMcmc::Bsr::Gibbs::BlockSamplerArima _.arm.blk;
  VMatrix standarize(VMatrix _Z) 
  { 
    _.arm.blk::filter(_Z)
  }
};

////////////////////////////////////////////////////////////////////////////
WorkSpace.ArimaReg WorkSpace.ArimaReg.Default(Real unused)
////////////////////////////////////////////////////////////////////////////
{
  BysMcmc::Bsr::Gibbs::WorkSpace.ArimaReg aux = 
  [[
    VMatrix _.Y = Rand(0,0,0,0);
    VMatrix _.X = Rand(0,0,0,0);
    BysMcmc::Bsr::Gibbs::BlockSamplerArima _.arm.blk = 
      BysMcmc::Bsr::Gibbs::BlockSamplerArima.Default(0)
  ]]
};


////////////////////////////////////////////////////////////////////////////
Class BlockSamplerMainLinear : BlockSamplerStdLinear 
////////////////////////////////////////////////////////////////////////////
{
  //--------------------------------------------------------------------------
  // Auxiliar members
  //--------------------------------------------------------------------------
  WorkSpace.ArimaReg _.ws = WorkSpace.ArimaReg.Default(0);

  //--------------------------------------------------------------------------
  //Methods
  //--------------------------------------------------------------------------

  ////////////////////////////////////////////////////////////////////////////
  WorkSpace.LinReg getWorkSpaceLinReg(Real unused) 
  ////////////////////////////////////////////////////////////////////////////
  { _.ws };

  ////////////////////////////////////////////////////////////////////////////
  Real _do_buildWorkSpace(VMatrix Y,    //Non filtered output
                          VMatrix X,    //Non filtered input
                          BysMcmc::Bsr::Gibbs::BlockSamplerArima arm.blk) 
  ////////////////////////////////////////////////////////////////////////////
  {
    BysMcmc::Bsr::Gibbs::WorkSpace.ArimaReg wsar = 
    [[
      VMatrix _.Y = Y;
      VMatrix _.X = X;
      BysMcmc::Bsr::Gibbs::BlockSamplerArima _.arm.blk = arm.blk
    ]];
    BysMcmc::Bsr::Gibbs::WorkSpace.ArimaReg _.ws := wsar;
    True
  };

  ////////////////////////////////////////////////////////////////////////////
  Real _buildWorkSpace(VMatrix Y,    //Non filtered output
                       VMatrix X,    //Non filtered input
                       BysMcmc::Bsr::Gibbs::BlockSamplerArima arm.blk) 
  ////////////////////////////////////////////////////////////////////////////
  {
  //WriteLn("TRACE"+_MID+" MainLinearBlock::_buildWorkSpace 0");
    If(!_.N1,
    {
    //WriteLn("TRACE"+_MID+" MainLinearBlock::_buildWorkSpace 1");
      _do_buildWorkSpace(Y,X,arm.blk)
    },
    {
    //WriteLn("TRACE"+_MID+" MainLinearBlock::_buildWorkSpace 2");
      VMatrix b1 = _.partialSampler::_.fixedValues;
      VMatrix X1 = SubCol(X, _.i1);
      VMatrix X2 = SubCol(X, _.i2);
      VMatrix Y2 = Y - X1*b1;
      _do_buildWorkSpace(Y2,X2,arm.blk)
    })
  }

};

//////////////////////////////////////////////////////////////////////////////
Text _.autodoc.member.MainLinearBlock =
"Builds a NameBlock that can draw a Gibbs sample of the Main Linear block "
"of a Bayesian Sparse Regression model";
//////////////////////////////////////////////////////////////////////////////
  BlockSamplerStdLinear MainLinearBlock(
    Text name, 
    Set  varNames, 
    BysMcmc::McmcPartialHandler mcmcPartialHandler,
    Real numBlock, 
    Real firstCol,
    BysMcmc::Config config)
//////////////////////////////////////////////////////////////////////////////
{
  If(!Card(varNames), 
  BysMcmc::Bsr::Gibbs::BlockSamplerStdLinear.Default(name,numBlock,firstCol,config),
  BysMcmc::Bsr::Gibbs::BlockSamplerMainLinear blk = [[

  Code _.buildWorkSpace = _buildWorkSpace;

  //Defining the block in the Markov Chain
  Real _defined = define(
    name, 
    varNames, 
    mcmcPartialHandler,
    numBlock, 
    firstCol, 
    config)

]])};
