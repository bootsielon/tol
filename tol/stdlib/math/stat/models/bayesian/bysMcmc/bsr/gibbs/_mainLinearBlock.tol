/////////////////////////////////////////////////////////////////////////////
// LIBRARY: StdtLib (Standard Library of TOL)
// MODULE: Stat (Statistics)
// FILE: _mainLinearBlock.tol
// PURPOSE: Declares method BysMcmc::Bsr::Gibbs::MainLinearBlock
/////////////////////////////////////////////////////////////////////////////

////////////////////////////////////////////////////////////////////////////
//Method _.buildWorkSpace must create _.ws as an instance of this class
Class WorkSpace.ArimaReg : WorkSpace.LinReg
////////////////////////////////////////////////////////////////////////////
{
  NameBlock _.arm.blk
};

//////////////////////////////////////////////////////////////////////////////
Text _.autodoc.member.MainLinearBlock =
"Builds a NameBlock that can draw a Gibbs sample of the Main Linear block "
"of a Bayesian Sparse Regression model";
//////////////////////////////////////////////////////////////////////////////
  StdLinearBlock MainLinearBlock(
    Text name, 
    Set varNames, 
    Real numBlock, 
    Real firstCol,
    Config config)
//////////////////////////////////////////////////////////////////////////////
{
  If(!Card(varNames), 
  BysMcmc::NoBlockSampler(name, varNames, numBlock, firstCol, config),
  BysMcmc::Bsr::Gibbs::StdLinearBlock blk = [[
  //Defining the block in the Markov Chain
  Real _define = define(name, varNames, numBlock, firstCol, config);

  ////////////////////////////////////////////////////////////////////////////
  Real _buildWorkSpace(VMatrix   Y,    //Non filtered output
                       VMatrix   X,    //Non filtered input
                       BysMcmc::BlockSampler arm.blk) //ArimaBlock filter
  ////////////////////////////////////////////////////////////////////////////
  {
    BysMcmc::Bsr::Gibbs::WorkSpace.ArimaReg wsar = 
    [[
      VMatrix _.Y = Y;
      VMatrix _.X = X;
      NameBlock _.arm.blk = arm.blk
    ]];
    NameBlock _.ws := wsar;
    True
  };
  Code _.buildWorkSpace = _buildWorkSpace;

  ////////////////////////////////////////////////////////////////////////////
  VMatrix standarize(VMatrix _Z) 
  ////////////////////////////////////////////////////////////////////////////
  { 
    _.ws::_.arm.blk::filter(_Z)
  }

]])};
