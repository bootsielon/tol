/////////////////////////////////////////////////////////////////////////////
// LIBRARY: StdtLib (Standard Library of TOL)
// MODULE: Stat (Statistics)
// FILE: _mainLinearBlock.tol
// PURPOSE: Declares method BysMcmc::Bsr::Gibbs::MainLinearBlock
/////////////////////////////////////////////////////////////////////////////

/////////////////////////////////////////////////////////////////////////////
// LIBRARY: StdtLib (Standard Library of TOL)
// MODULE: Stat (Statistics)
// FILE: _mainLinearBlock.tol
// PURPOSE: Declares method BysMcmc::Bsr::Gibbs::MainLinearBlock
/////////////////////////////////////////////////////////////////////////////

////////////////////////////////////////////////////////////////////////////
//Method buildWorkSpace must create _.ws as an instance of this class
Class @WorkSpace.MainLinear : @WorkSpace.LinReg
////////////////////////////////////////////////////////////////////////////
{
  Text get.blockSamplerClassName(Real void) { "@BlockMainLinear" };
  Set _.armBlkRef;
  
  //////////////////////////////////////////////////////////////////////////
  VMatrix standarize(VMatrix _Z) 
  //////////////////////////////////////////////////////////////////////////
  { 
    (_.armBlkRef[1])::filter(_Z)
  };

  //////////////////////////////////////////////////////////////////////////
  Static @WorkSpace.MainLinear Null(Real unused)
  //////////////////////////////////////////////////////////////////////////
  {
    BysMcmc::Bsr::Gibbs::@WorkSpace.MainLinear new = 
    [[
      VMatrix _.Y = Rand(0,0,0,0);
      VMatrix _.X = Rand(0,0,0,0);
      Set _.armBlkRef = Copy(Empty)
    ]]
  };

  //////////////////////////////////////////////////////////////////////////
  Static @WorkSpace.MainLinear New(
    VMatrix Y, 
    VMatrix X,    
    BysMcmc::Bsr::Gibbs::@BlockArima arm.blk )
  //////////////////////////////////////////////////////////////////////////
  {
    BysMcmc::Bsr::Gibbs::@WorkSpace.MainLinear new = [[
      VMatrix _.Y = Y,    
      VMatrix _.X = X,    
      Set _.armBlkRef = [[arm.blk]] ]]
  }

};


////////////////////////////////////////////////////////////////////////////
Class @BlockMainLinear : @BlockStdLin 
////////////////////////////////////////////////////////////////////////////
{
  //--------------------------------------------------------------------------
  // Auxiliar members
  //--------------------------------------------------------------------------
  @WorkSpace.MainLinear _.ws = 
    BysMcmc::Bsr::Gibbs::@WorkSpace.MainLinear::Null(0);

  //--------------------------------------------------------------------------
  //Methods
  //--------------------------------------------------------------------------

  ////////////////////////////////////////////////////////////////////////////
  @WorkSpace.LinReg getWorkSpaceLinReg(Real unused) 
  ////////////////////////////////////////////////////////////////////////////
  { _.ws };

  ////////////////////////////////////////////////////////////////////////////
  Real _do_buildWorkSpace(VMatrix Y,    //Non filtered output
                          VMatrix X,    //Non filtered input
                          BysMcmc::Bsr::Gibbs::@BlockArima arm.blk) 
  ////////////////////////////////////////////////////////////////////////////
  {
    VMatrix _.ws::_.Y := Y;
    VMatrix _.ws::_.X := X;
    Set  _.ws::_.armBlkRef := [[arm.blk]];
    True
  };

  ////////////////////////////////////////////////////////////////////////////
  Real _buildWorkSpace(@WorkSpace.MainLinear param) 
  ////////////////////////////////////////////////////////////////////////////
  {
  //WriteLn("TRACE"+_MID+" MainLinearBlock::_buildWorkSpace 0");
    If(!_.N1,
    {
    //WriteLn("TRACE"+_MID+" MainLinearBlock::_buildWorkSpace 1");
      _do_buildWorkSpace(param::_.Y, param::_.X, (param::_.armBlkRef)[1])
    },
    {
    //WriteLn("TRACE"+_MID+" MainLinearBlock::_buildWorkSpace 2");
      VMatrix b1 = SubRow(_.partialSampler::_.values, _.i1);
      VMatrix X1 = SubCol(param::_.X, _.i1);
      VMatrix X2 = SubCol(param::_.X, _.i2);
      VMatrix Y2 = param::_.Y - X1*b1;
      _do_buildWorkSpace(Y2, X2, (param::_.armBlkRef)[1])
    })
  };
  ////////////////////////////////////////////////////////////////////////////
  Real buildWorkSpace(@WorkSpace param) 
  ////////////////////////////////////////////////////////////////////////////
  {
    _buildWorkSpace(param)
  };

  //////////////////////////////////////////////////////////////////////////////
  Static Text _.autodoc.member.New =
  "Builds a NameBlock that can draw a Gibbs sample of the Main Linear block "
  "of a Bayesian Sparse Regression model";
  //////////////////////////////////////////////////////////////////////////////
  Static @BlockStdLin New(
    Text name, 
    Set  varNames, 
    BysMcmc::@McmcPartialHandler mcmcPartialHandler,
    Real numBlock, 
    Real firstCol,
    BysMcmc::@Config config)
  //////////////////////////////////////////////////////////////////////////////
  {
    If(!Card(varNames), 
    BysMcmc::Bsr::Gibbs::@BlockStdLinEmpty::New(name,numBlock,firstCol,config),
    BysMcmc::Bsr::Gibbs::@BlockMainLinear blk = [[

    //Defining the block in the Markov Chain
    Real _defined = define(
      name, 
      varNames, 
      mcmcPartialHandler,
      numBlock, 
      firstCol, 
      config)

  ]])}

};

