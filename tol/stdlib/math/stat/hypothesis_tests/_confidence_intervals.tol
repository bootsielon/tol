/////////////////////////////////////////////////////////////////////////////
// ARCHIVO     : conflim.tol
// DESCRIPCION : Funciones para el cáculo de límites de
//               confianza sobre poblaciones normales.
//////////////////////////////////////////////////////////////////////////////


//////////////////////////////////////////////////////////////////////////////
// FUNCIONES
//////////////////////////////////////////////////////////////////////////////


//////////////////////////////////////////////////////////////////////////////
  Real Chequea(Real n, Real colas, Real alfa, Real ratio)
//////////////////////////////////////////////////////////////////////////////
{
  Real chN =
  If
  (
    LE(5,n) & (n==Floor(n)),
    1,
    ErrorMsg("El número de datos ha de ser un entero mayor o igual que 5")
  );
  Real chColas =
  If
  (
    (colas==1)|(colas==2),
    1,
    ErrorMsg("El número de colas ha de ser 1 ó 2")
  );
  Real chAlfa =
  If
  (
    LE(0,alfa,1),
    1,
    ErrorMsg("El nivel significación ha de ser un real entre 0 y 1")
  );
  Real chRatio =
  If
  (
    LE(0,ratio,1),
    1,
    ErrorMsg("La proporción o ratio para valores individuales "+
             "ha de ser un real entre 0 y 1")
  );
  chN & chColas & chAlfa & chRatio
};
//////////////////////////////////////////////////////////////////////////////
  PutDescription(
  "Chequea la validez de los parámetros, los cuales han de cumplir lo "+
  "siguiente :\n"+
  "  El número de datos ha de ser un entero mayor o igual que 5\n"+
  "  El número de colas ha de ser 1 ó 2\n"+
  "  El nivel significación ha de ser un real entre 0 y 1\n"+
  "  La proporción o ratio para valores individuales \n"+
  "ha de ser un real entre 0 y 1",
  Chequea);
//////////////////////////////////////////////////////////////////////////////



//////////////////////////////////////////////////////////////////////////////
  Real T(Real n, Real colas, Real alfa)
//////////////////////////////////////////////////////////////////////////////
{
  DistTInv(1-alfa/colas,n)
};
//////////////////////////////////////////////////////////////////////////////
  PutDescription(

  "Dado cierto nivel de significación, alfa, y un número de grados de "+
  "libertad, n, la función T calcula el valor de la variable t de Student "+
  "que contiene una probabilidad 1-alfa : \n"+
  "Por debajo del valor t obtenido, si la prueba es unilateral (número de "+
  "colas = 1)."+
  "Entre -t y t si la prueba es bilateral (numero de colas = 2).\n"+
  "Dicho de otro modo la función  T, con n grados de libertad, devuelve "+
  "el valor t tal que \n"+
  "  Prob(x<=t) = 1-alfa  y se expresa T(n,1,alfa) = t, o \n"+
  "  Prob(-t<=x<=t) = 1-alfa y se expresa T(n,2,alfa) = t\n" +
  "donde x se comporta como una t de Student con n grados de libertad.",
  T);
//////////////////////////////////////////////////////////////////////////////



//////////////////////////////////////////////////////////////////////////////
  Real Limsm(Set datos, Real colas, Real alfa)
//////////////////////////////////////////////////////////////////////////////
{
  Real n = Card(datos);
  If
  (
    Chequea(n,colas,alfa,0),
    SetAvr(datos)+T(n,colas,alfa)*StDsM(datos)/Sqrt(n),
    0
  )
};
//////////////////////////////////////////////////////////////////////////////
  PutDescription(
  "Devuelve el límite superior del intervalo de confianza para la media, "+
  "de una muestra extraída de una población normal, en el caso en el "+
  "que desconocemos la media y la desviación típica y tomamos como "
  "estimadores insesgados la media poblacional y la cuasi-desviación "
  "típica respectivamente.\n"+
  "Si el número de colas es igual a 1 existe una probabilidad 1-alfa de "+
  "que la media poblacional esté por debajo del límite superior al que "
  "llamaremos unilateral.\n"+
  "Si el número de colas es igual a 2 existe una probabilidad (1-alfa) "+
  "de que la media poblacional esté entre los límites bilaterales inferior "
  "y superior. El límite inferior bilateral se calcula con la función Limim "
  "con número de colas igual a 2.

Sintaxis

Limsm([[{lista de datos}]], c, alfa)

{lista de datos}: Datos muestrales con separador ',' y formato númerico  de "
"separador decimal'.'. Se requieren al menos 5 valores

 c:    especifica el número de colas. Si c=1, calcula la distribución para
       una cola (unilateral) y si c=2 la calcula para 2 colas (bilateral)

 alfa: especifica el nivel de significación, (0 <= alfa <= 1)",

  Limsm);
//////////////////////////////////////////////////////////////////////////////



//////////////////////////////////////////////////////////////////////////////
  Real Limim(Set datos, Real colas, Real alfa)
//////////////////////////////////////////////////////////////////////////////
{
  Real n = Card(datos);
  If
  (
    Chequea(n,colas,alfa,0),
    SetAvr(datos)-T(n,colas,alfa)*StDsM(datos)/Sqrt(n),
    0
  )
};
//////////////////////////////////////////////////////////////////////////////
  PutDescription(
  "Devuelve el límite inferior del intervalo de confianza para la media, "+
  "de una muestra extraída de una población normal, en el caso en el "+
  "que desconocemos la media y la desviación típica y tomamos como "
  "estimadores insesgados la media poblacional y la cuasi-desviación "
  "típica respectivamente.\n"+
  "Si el número de colas es igual a 1 existe una probabilidad 1-alfa de "+
  "que la media poblacional esté por encima del límite superior al que "
  "llamaremos unilateral.\n"+
  "Si el número de colas es igual a 2 existe una probabilidad (1-alfa) "+
  "de que la media poblacional esté entre los límites bilaterales inferior "
  "y superior. El límite superior bilateral se calcula con la función Limsm "
  "con número de colas igual a 2.

Sintaxis

Limim([[{lista de datos}]], c, alfa)

{lista de datos}: Datos muestrales con separador ',' y formato númerico  de "
"separador decimal'.'. Se requieren al menos 5 valores

 c:    especifica el número de colas. Si c=1, calcula la distribución para
       una cola (unilateral) y si c=2 la calcula para 2 colas (bilateral)

 alfa: especifica el nivel de significación, (0 <= alfa <= 1)",
  Limim);
//////////////////////////////////////////////////////////////////////////////

//////////////////////////////////////////////////////////////////////////////
  Real KSigma(Real n, Real colas, Real alfa)
//////////////////////////////////////////////////////////////////////////////
{
   Real C = If
    (
      colas==1,
      DistChiInv(1-alfa,n-1),
      NewtonSolve(Real (Real x)
      {
        Real y  = 2*v-x;
        Real Fx = DistChi(x,v);
        Real Fy = DistChi(y,v);
        Real F2 = Fy-Fx;
  //    WriteLn("x="<<x<<" y="<<y<<" Fx="<<Fx<<" Fy="<<Fy<<" F2="<<F2);
        F2-(1-alfa)
      }, DistChiInv(1-alfa/2,v))
    );
    Sqrt((n-1)/C)
};
//////////////////////////////////////////////////////////////////////////////
  PutDescription(
  "El factor KSig que se utiliza para determinar, para una probabilidad dada "
  "el intervalo (inferior o superior) para la desviacón típica de la "
  "distribución de una población normal. El factor KSig se calcula para "
  "casos unilaterales y bilaterales usando el número de colas igual a 1 ó a "
  "2 respectivamente. Los cálculos se basan en que el cociente entre la "
  "varianza muestral y la poblacional tiene por distribución 1/(n-1) veces "
  "una chi cuadrado con (n-1) grados de libertad.",
  KSigma);
//////////////////////////////////////////////////////////////////////////////

//////////////////////////////////////////////////////////////////////////////
  Real Limss(Set datos, Real colas, Real alfa)
//////////////////////////////////////////////////////////////////////////////
{
  Real n = Card(datos);
  If(Chequea(n,colas,alfa,0),StDsM(datos)*KSigma(n,colas,alfa),0)
};
//////////////////////////////////////////////////////////////////////////////
  PutDescription(
  "Devuelve el límite superior del intervalo de confianza para la "
  "desviación, típica de una muestra extraída de una población normal."
  "Si el número de colas es igual a 1 existe una probabilidad 1-alfa de "+
  "que la desviación poblacional esté por encima del límite superior al que "
  "llamaremos unilateral.\n"+
  "Si el número de colas es igual a 2 existe una probabilidad (1-alfa) "+
  "de que la desviación poblacional esté entre los límites bilaterales "
  "inferior y superior. El límite superior bilateral se calcula con la "
  "función Limis con número de colas igual a 2. \n"
  "Los cálculos se basan en que el cociente entre la "
  "varianza muestral y la poblacional tiene por distribución 1/(n-1) veces "
  "una chi cuadrado con (n-1) grados de libertad.

Sintaxis

Limss([[{lista de datos}]], c, alfa)

{lista de datos}: Datos muestrales con separador ',' y formato númerico  de "
"separador decimal'.'. Se requieren al menos 5 valores

 c:    especifica el número de colas. Si c=1, calcula la distribución para
       una cola (unilateral) y si c=2 la calcula para 2 colas (bilateral)

 alfa: especifica el nivel de significación, (0 <= alfa <= 1)",

   Limss);
//////////////////////////////////////////////////////////////////////////////

//////////////////////////////////////////////////////////////////////////////
  Real Limis(Set datos, Real colas, Real alfa)
//////////////////////////////////////////////////////////////////////////////
{
  Real n = Card(datos);
  If(Chequea(n,colas,alfa,0),StDsM(datos)*KSigma(n,colas,1-alfa),0)
};
//////////////////////////////////////////////////////////////////////////////
  PutDescription(
  "Devuelve el límite inferior del intervalo de confianza para la "
  "desviación, típica de una muestra extraída de una población normal."
  "Si el número de colas es igual a 1 existe una probabilidad 1-alfa de "+
  "que la desviación poblacional esté por debajo del límite inferior al que "
  "llamaremos unilateral.\n"+
  "Si el número de colas es igual a 2 existe una probabilidad (1-alfa) "+
  "de que la desviación poblacional esté entre los límites bilaterales "
  "inferior y superior. El límite superior bilateral se calcula con la "
  "función Limis con número de colas igual a 2.\n"
  "Los cálculos se basan en que el cociente entre la "
  "varianza muestral y la poblacional tiene por distribución 1/(n-1) veces "
  "una chi cuadrado con (n-1) grados de libertad.

Sintaxis

Limis([[{lista de datos}]], c, alfa)

{lista de datos}: Datos muestrales con separador ',' y formato númerico  de "
"separador decimal'.'. Se requieren al menos 5 valores

 c:    especifica el número de colas. Si c=1, calcula la distribución para
       una cola (unilateral) y si c=2 la calcula para 2 colas (bilateral)

 alfa: especifica el nivel de significación, (0 <= alfa <= 1)",
  Limss);
//////////////////////////////////////////////////////////////////////////////

//////////////////////////////////////////////////////////////////////////////
// FILE    :
// PURPOSE :
//////////////////////////////////////////////////////////////////////////////

//////////////////////////////////////////////////////////////////////////////
  Real K(Real n, Real colas, Real alfa, Real ratio)
//////////////////////////////////////////////////////////////////////////////
{
  If
  (
    colas==1,
    {
      Real d = Sqrt(n)*DistNormalInv(ratio);
      DistNCTInv(1-alfa,n-1,d)/Sqrt(n)
    },
    {
      Real rn1 = 1/Sqrt(n);
      Real C   = Sqrt((n-1)/DistChiInv(alfa,n-1));
      Real x   = NewtonSolve(Real (Real x)
      {
        DistNormal(rn1+x)-DistNormal(rn1-x)-ratio
      }, 1.2*K(n,1,alfa,ratio)/C);
      x*C
    }
  ) 
};
//////////////////////////////////////////////////////////////////////////////
  PutDescription(
  "El factor K que se utiliza para determinar, para una probabilidad y "
  "fracción de la población dadas, el intervalo de tolerancia (inferior "
  "o superior) de la distribución para valores individuales de una población "
  "normal. El factor K se calcula para casos unilaterales y bilaterales.

Sintaxis

K(n, c, alfa, P)

n: numero de datos muestra. Se requiere n³5
 c:   especifica el número de colas. Si c=1, calcula la distribución para "
 "una cola (unilateral) y si c=2 la calcula para 2 colas (bilateral)

alfa: especifica el nivel de significación, (0 <= alfa <= 1)

 P: especifica la proporción  o fracción (0 <= P <= 1)

Definición

K se define como el factor para el que con una probabilidad dada (1-alfa) "
"al menos una fracción de la población (P) sea menor que <x>+ K s ( o mayor "
"que <x> - K s) siendo '<x>' la media de la muestra y 's' su desviación "
"estandar calculados a partir de una muestra de tamaño n. Esta función hace "
"el calculo para dos casos: a) La fracción de la población elegida quede "
"centrada en un intervalo, <x>± K s, simetrico respecto de la media "
"(caso bilateral o de dos colas),  y b)  La fracción de la población "
"elegida quede toda ella por debajo <x>+ K s o por encima de <x>- K s "
"(caso unilaral o de una cola).

Ejemplos

1 Calcular K para una muestra de tamaño n, tal que con una probabilidad del "
"gamma al menos una fracción P de la población quede por debajo <x>+ K s.

a) Para n=5, gamma = 0.95, P = 0.95. Como gamma= 1- alfa, alfa = 0.05

K(5, 1, 0.05, 0.95) = 4.20661

b) Para n=50, gamma = 0.75, P = 0.90.

K(50, 1, 0.25, 0.90) = 1.42493

2 Calcular K para una muestra de tamaño n, tal que con una probabilidad del "
"gamma al menos una fracción P de la población este comprendida en el "
"intervalo <x>± K s.

a) Para n=5, gamma = 0.75, P = 0.95.

K(5, 2, 0.25, 0.95) = 3.0879

b) Para n=50, gamma = 0.95, P = 0.90.

K(50, 2, 0.05, 0.90) = 1.99634

Nota: Observese que hay que utilizar 'alfa' en vez de la probabilidad "
"'gamma', siendo 'alfa = 1- gamma'",
  K);
//////////////////////////////////////////////////////////////////////////////



//////////////////////////////////////////////////////////////////////////////
  Real Limsx(Set datos, Real colas, Real alfa, Real ratio)
//////////////////////////////////////////////////////////////////////////////
{
  Real n = Card(datos);
  If
  (
    Chequea(n,colas,alfa,ratio),
    SetAvr(datos)+K(n,colas,alfa,ratio)*StDsM(datos),
    0
  )
};
//////////////////////////////////////////////////////////////////////////////
  PutDescription(
"Calcula el límite superior (LS) del intervalo de tolerancia de la "
"distribución para valores individuales con una probabilidad (1-alfa) "
"definida para la fracción de la población (P) que se desee quede por "
"debajo de dicho límite, y todo ello partiendo de n valores individuales "
"muestrales. Esta función hace el calculo para dos casos: a) La fracción de "
"la población elegida quede centrada en un intervalo simetrico respecto de "
"la media (caso bilateral o de dos colas), en este caso LS corresponde al "
"limite superior del intervalo,  y b)  La fracción de la población elegida "
"quede toda ella por debajo del LS (caso unilaral o de una cola)..

Sintaxis

Limsx([[{lista de datos}]], c, alfa, P)

{lista de datos}: Datos muestrales con separador ',' y formato númerico  de "
"separador decimal'.'. Se requieren al menos 5 valores

  c:    especifica el número de colas. Si c=1, calcula la distribución para
        una cola (unilateral) y si c=2 la calcula para 2 colas (bilateral)

  alfa: especifica el nivel de significación, (0 <= alfa <= 1)

  P:    especifica la proporción  o fracción (0 <= P <= 1)

Definición

LS = <x> + K s, donde

'<x>'  el estimador de la media y 's' el estimador de la desviación estandar "
"muestral calculados a partir de los 'n' valores de la muestra de la 'lista "
"de datos' (set de datos).

K  es una función de 'c', 'alfa',  'P', y del numero de valores 'n'. Es "
"decir K(c, alfa,P,n).  Se interpreta como sigue:

Para c=1,  o de una cola, es aquel valor (límite unilateral superior) para "
"el que existe una probabilidad  'gamma=1-alfa' de que al menos una "
"proporción 'P' de la distribución sea menor o igual  que <x> + K s

Para c=2,  o de dos colas, es aquel valor (límite bilateral superior) para "
"el que existe una probabilidad 'gamma=1-alfa' de que al menos una "
"proporción 'P' de la distribución esté comprendida entre <x> + K s y su "
"simetrico inferior <x> - K s, que puede calcularse con Limix

Ejemplos

1. Calcular el Limite superior de tolerancia (LS) para la lista de valores "
"{2.1, 3.2, 4.3, 7.1, 8.2}, para el caso unilateral, tal que con una "
"probabilidad del 95% al menos el 95% de la población quede por debajo de "
"dicho valor.

Limsx([[2.1, 3.2, 4.3, 7.1, 8.2]], 1, 0.05, 0.95) = 15.8661

2. Con la lista de valores anterior,  calcular el Limite Superior de "
"tolerancia (LS) para que con una probabilidad del 90% el 95% de la "
"población quede comprendida entre este valor LS  y su simetrico inferior

Limsx([[2.1, 3.2, 4.3, 7.1, 8.2]], 2, 0.1, 0.90) = 14.0232

Nota: Observese que hay que utilizar 'alfa' en vez de la probabilidad "
"'gamma', siendo 'alfa = 1- gamma'"
,
  Limsx);
//////////////////////////////////////////////////////////////////////////////



//////////////////////////////////////////////////////////////////////////////
  Real Limix(Set datos, Real colas, Real alfa, Real ratio)
//////////////////////////////////////////////////////////////////////////////
{
  Real n = Card(datos);
  If
  (
    Chequea(n,colas,alfa,ratio),
    SetAvr(datos)-K(n,colas,alfa,ratio)*StDsM(datos),
    0
  )
};
//////////////////////////////////////////////////////////////////////////////
  PutDescription(
  "Calcula el límite inferior (LI) del intervalo de tolerancia de la "
  "distribución para valores individuales con una probabilidad (1-alfa) "
  "definida para la fracción de la población (P) que se desee quede por "
  "encima de dicho límite, y todo ello partiendo de n valores individuales "
  "muestrales. Esta función hace el calculo para dos casos: a) La fracción "
  "de la población elegida quede centrada en un intervalo simetrico respecto "
  "de la media (caso bilateral o de dos colas), en este caso LI corresponde "
  "al limite inferior del intervalo,  y b)  La fracción de la población "
  "elegida quede toda ella por encima del LI (caso unilaral o de una cola)..

Sintaxis

Limix([[{lista de datos}]], c, alfa, P)

{lista de datos}: Datos muestrales con separador ',' y formato númerico  de "
"separador decimal'.'. Se requieren al menos 5 valores

 c:    especifica el número de colas. Si c=1, calcula la distribución para
       una cola (unilateral) y si c=2 la calcula para 2 colas (bilateral)

 alfa: especifica el nivel de significación, (0 <= alfa <= 1)

 P:    especifica la proporción  o fracción (0 <= P <= 1)

Definición

LS = <x> - K s, donde

'<x>'  el estimador de la media y 's' el estimador de la desviación estandar "
"muestral calculados a partir de los 'n' valores de la muestra de la "
"'lista de datos' (set de datos).

K  es una función de 'c', 'alfa',  'P', y del numero de valores 'n'. Es "
"decir K(c, alfa,P,n).  Se interpreta como sigue:

Para c=1,  o de una cola, es aquel valor (límite unilateral inferior) para "
"el que existe una probabilidad  'gamma=1-alfa' de que al menos una "
"proporción 'P' de la distribución sea mayor o igual  que <x> + K s

Para c=2,  o de dos colas, es aquel valor (límite bilateral inferior) para "
"el que existe una probabilidad  'gamma=1-alfa' de que al menos una "
"proporción 'P' de la distribución esté comprendida entre  <x> - K s y su "
"simetrico superior <x> + K s, que puede calcularse con Limsx

Ejemplos

1. Calcular el Limite inferior de tolerancia (LS) para la lista de valores "
"{2.1, 3.2, 4.3, 7.1, 8.2}, para el caso unilateral, tal que con una "
"probabilidad del 95% al menos el 95% de la población quede por encima de "
"dicho valor.

Limix([[2.1, 3.2, 4.3, 7.1, 8.2]], 1, 0.05, 0.95) = -5.09611

2. Con la lista de valores anterior,  calcular el Limite Inferior de "
"tolerancia (LI) para que con una probabilidad del 90% el 95% de la "
"población quede comprendida entre este valor LI  y su simetrico inferior

Limix([[2.1, 3.2, 4.3, 7.1, 8.2]], 2, 0.1, 0.95) = -5.76359

Nota: Observese que hay que utilizar 'alfa' en vez de la probabilidad "
"'gamma', siendo 'alfa = 1- gamma'",
  Limix);
//////////////////////////////////////////////////////////////////////////////





