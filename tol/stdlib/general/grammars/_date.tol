//////////////////////////////////////////////////////////////////////////////
// FILE    : _date.tol
// PURPOSE : Funciones generales de date 
//////////////////////////////////////////////////////////////////////////////

//////////////////////////////////////////////////////////////////////////////
// FUNCTIONS
//////////////////////////////////////////////////////////////////////////////

//////////////////////////////////////////////////////////////////////////////
Date ComNotNullN(Serie ser, Real rea)
//////////////////////////////////////////////////////////////////////////////
{
  TimeSet serTS     = SerTms(ser);
  Set     dateNoNul = Dates(serTS, First(ser), Last(ser));
  Date    fecref    = If(Eq(Sign(rea),1), dateNoNul[1],
                                        dateNoNul[Card(dateNoNul)]);
  Date    fecprop   = Succ(fecref, serTS, rea-Sign(rea));
  If(Belong(fecprop,serTS), fecprop,
                            If(Eq(Sign(rea),1), TheBegin, TheEnd))
};
//////////////////////////////////////////////////////////////////////////////
PutDescription("
Devuelve la fecha del n-esimo elemento no nulo de una serie. Si contamos desde
el final de la serie n debe ser negativo, por ejemplo, -1 indica la ultima
ocurrencia de un valor no nulo.",
ComNotNullN);
//////////////////////////////////////////////////////////////////////////////

//////////////////////////////////////////////////////////////////////////////
  Date SetMinDate(Set setDate) 
//////////////////////////////////////////////////////////////////////////////
{ 
  Real c = Card(setDate);
  Case
  (
    EQ(c,0), ?,
    EQ(c,1), setDate[1], 
    1,       Group("Min",setDate)
  ) 
};
//////////////////////////////////////////////////////////////////////////////
PutDescription(
"Devuelve la menor de las fechas del conjunto de fechas que recibe",
SetMinDate);
//////////////////////////////////////////////////////////////////////////////

//////////////////////////////////////////////////////////////////////////////
  Date SetMaxDate(Set setDate) 
//////////////////////////////////////////////////////////////////////////////
{ 
  Real c = Card(setDate);
  Case
  (
    EQ(c,0), ?,
    EQ(c,1), setDate[1], 
    1,       Group("Max",setDate)
  ) 
};
//////////////////////////////////////////////////////////////////////////////                            
PutDescription(
"Devuelve la mayor de las fechas del conjunto de fechas que recibe",
SetMaxDate);
//////////////////////////////////////////////////////////////////////////////

//////////////////////////////////////////////////////////////////////////////
Date ComFirstEqual(Serie ser, Real rea)
//////////////////////////////////////////////////////////////////////////////
{
  Date ini            = First(ser);
  TimeSet fech        = Dating(ser);
  Serie valorSer      = CalInd(C, fech)*rea;
  Serie dif           = ser-valorSer;
  Serie noNulo        = Not(dif);
  Date firstDateEqual = FirstNotEqual(noNulo,ini,0);
  firstDateEqual
};
//////////////////////////////////////////////////////////////////////////////
PutDescription(
"Devuelve la fecha de la serie que tiene ese valor",
ComFirstEqual);
//////////////////////////////////////////////////////////////////////////////

//////////////////////////////////////////////////////////////////////////////
Date KnownDate(Date dte)
//////////////////////////////////////////////////////////////////////////////
{
  Case
  (
    dte == TheBegin, DefFirst, 
    dte == TheEnd  , DefLast,
    dte == UnknownDate, Now,
    1, dte
  )  
};
//////////////////////////////////////////////////////////////////////////////
PutDescription(
"Retorna una fecha conocida a partir de una fecha dada.",
KnownDate);
//////////////////////////////////////////////////////////////////////////////

//////////////////////////////////////////////////////////////////////////////
  Date LastNotEqual(Serie sr, Date desde, Real valor)
//////////////////////////////////////////////////////////////////////////////
{
  Serie sub = SubSer(sr, First(sr), desde);
  Serie rev = ReverseSer(sub);
  Date d    = FirstNotEqual(rev, First(rev), valor);
  Real num  = DateDif(Dating(rev), First(rev), d);
  Succ(Last(rev), Dating(sr), -num)
};
//////////////////////////////////////////////////////////////////////////////
PutDescription("
Devuelve la ultima fecha a partir de una dada para la que el valor de una
serie es distinto de un valor dado.",
LastNotEqual);
//////////////////////////////////////////////////////////////////////////////


