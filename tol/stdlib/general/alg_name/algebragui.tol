//////////////////////////////////////////////////////////////////////////////
// FILE   : algebragui.tol
// PURPOSE: Funciones de gestion del algebra para el interface de usuario
//////////////////////////////////////////////////////////////////////////////

//////////////////////////////////////////////////////////////////////////////
// FUNCTIONS
//////////////////////////////////////////////////////////////////////////////

//////////////////////////////////////////////////////////////////////////////
Text GuiAlgGetDef(Set algDef) 
//////////////////////////////////////////////////////////////////////////////
{ 
  Real numCarDim     = algDef->NumCarDim;
  Text funAlgGetData = algDef->FunAlgGetData;
  Text dimSep        = algDef->DimSep;
  Text subDimSep     = algDef->SubDimSep;

  Set tclSetPre = SetOfSet(
    @TclArgSt("-numCarDim",     IntText(numCarDim)),
    @TclArgSt("-funAlgGetData", funAlgGetData),
    @TclArgSt("-dimSep",        dimSep),
    @TclArgSt("-sDiSep",        subDimSep)
  );
  Set tclSet         = tclSetPre << algDef->TclSet;
  TxtListTcl(BinGroup("+",tclSet))
};
//////////////////////////////////////////////////////////////////////////////
PutDescription("Devuelve las definiciones generales del algebra.",
GuiAlgGetDef);
//////////////////////////////////////////////////////////////////////////////

//////////////////////////////////////////////////////////////////////////////
Text GuiAlgGetMag(Set algDef) 
//////////////////////////////////////////////////////////////////////////////
{ 
  TxtListTcl(
    ExtractSetField(
      algDef->SetNode,
      SetOfText("CodMag","Nam","Des")
    )
  )
};
//////////////////////////////////////////////////////////////////////////////
PutDescription("Dado la definición de un algebra retorna la magnitudes
definidas.",
GuiAlgGetMag);
//////////////////////////////////////////////////////////////////////////////

//////////////////////////////////////////////////////////////////////////////
Text GuiAlgGetDim(
  Text codMag,    // Código de magnitud
  Set algDef      // Definición del algebra
)
//////////////////////////////////////////////////////////////////////////////
{
  Set algDefSetNode = AlgDefGetReg(codMag, algDef)->SetNode;
  Set dims          = AlgDefGetNode("*", "", algDefSetNode);
  Text TxtListTcl(
    ExtractSetField(
      dims,
      SetOfText("CodDim","Nam","Des","Req","Typ")
    )
  )
};
//////////////////////////////////////////////////////////////////////////////
PutDescription("Dada una magnitud y la definición de un algebra retorna las 
dimensiones que dependen de la misma.",
GuiAlgGetDim);
//////////////////////////////////////////////////////////////////////////////

//////////////////////////////////////////////////////////////////////////////
Text GuiAlgGetSubDim(Text codMag, Text codDim, Set algDef)
//////////////////////////////////////////////////////////////////////////////
{
  Set algDefSetNode = AlgDefGetReg(codMag, algDef)->SetNode;
  Set subDims       = AlgDefGetNode(codDim, "Not_", algDefSetNode);
  Text TxtListTcl(
    ExtractSetField(
      subDims,
      SetOfText("CodSubDim","Nam","Des","Req","Typ")
    )
  )
};
//////////////////////////////////////////////////////////////////////////////
PutDescription("Dado una magnitud, una dimension y la definición de un
algebra retorna las subdimensiones de la misma.",
GuiAlgGetSubDim);
//////////////////////////////////////////////////////////////////////////////

//////////////////////////////////////////////////////////////////////////////
Text GuiAlgGetSubDimVal
(
  Text codMag, 
  Text codDim, 
  Text codSubDim, 
  Set algSetNode, 
  Set algDef
)
//////////////////////////////////////////////////////////////////////////////
{
  Set algDefSetNode = AlgDefGetReg(codMag, algDef)->SetNode;
  Set valSet    = 
   AlgDefEvalValDep(codDim, codSubDim, algSetNode, algDefSetNode);
  TxtListTcl(valSet)
};
//////////////////////////////////////////////////////////////////////////////
PutDescription("Dados una magnitud, una dimension y una subdimension 
retorna los posibles valores que toma en funcion de las
dependencias especificadas en algDef para los valores de un algSetNode.", 
GuiAlgGetSubDimVal);
//////////////////////////////////////////////////////////////////////////////

//////////////////////////////////////////////////////////////////////////////
Text GuiAlgGetSubDimDefVal
(
  Text codMag, 
  Text codDim, 
  Text codSubDim, 
  Set algSetNode, 
  Set algDef
)
//////////////////////////////////////////////////////////////////////////////
{
  Set algDefSetNode = AlgDefGetReg(codMag, algDef)->SetNode;
  Text defVal    = 
   AlgDefEvalDefVal(codDim, codSubDim, algSetNode, algDefSetNode);
  defVal
//  TxtListTcl(valSet)
};
//////////////////////////////////////////////////////////////////////////////
PutDescription("Dados una magnitud, una dimension y una subdimension 
retorna el valor por defecto que toma en funcion de las
dependencias especificadas en algDef para los valores de un algSetNode.", 
GuiAlgGetSubDimDefVal);
//////////////////////////////////////////////////////////////////////////////

//////////////////////////////////////////////////////////////////////////////
Real GuiAlgGetDepSubDim(Text codMag, Text codDim, Text codSubDim, Set algDef)
//////////////////////////////////////////////////////////////////////////////
{
  Set algDefSetNode = AlgDefGetReg(codMag, algDef)->SetNode;
  Set depExi = AlgDefGetNodeByExiDep(codDim, codSubDim, "", algDefSetNode, 0);
  Set depVal = AlgDefGetNodeByValDep(codDim, codSubDim, algDefSetNode);
  Card(depExi)+Card(depVal)
};
//////////////////////////////////////////////////////////////////////////////
PutDescription("Dados una magnitud, una dimension y una subdimension 
y la definicion de un algebra devuelve el cardinal del conjunto 
de dimensiones o subdimensiones que depende existencialmente o por valor de 
los argumentos dados.", 
GuiAlgGetDepSubDim);
//////////////////////////////////////////////////////////////////////////////

//////////////////////////////////////////////////////////////////////////////
Text GuiAlgGetExiDepSubDim
(
  Text codMag,
  Text codDim, 
  Text codSubDim, 
  Set algDef
)
//////////////////////////////////////////////////////////////////////////////
{
  Set algDefSetNode = AlgDefGetReg(codMag, algDef)->SetNode;
  Set depExi = AlgDefGetNodeByExiDep(codDim, codSubDim, "", algDefSetNode, 0);
  TxtListTcl(ExtractSetField(depExi,SetOfText("CodDim","CodSubDim")))
};
//////////////////////////////////////////////////////////////////////////////
PutDescription("Dados una magnitud, una dimension y una subdimension y la 
definicion de un algebra devuelve el conjunto de nodos
que depende existencialmente del par dimension y una subdimension dado.",
GuiAlgGetExiDepSubDim);
//////////////////////////////////////////////////////////////////////////////

//////////////////////////////////////////////////////////////////////////////
Text GuiAlgGetValDepSubDim
(
  Text codMag,
  Text codDim, 
  Text codSubDim, 
  Set algDef
)
//////////////////////////////////////////////////////////////////////////////
{
  Set algDefSetNode = AlgDefGetReg(codMag, algDef)->SetNode;
  Set depVal = AlgDefGetNodeByValDep(codDim, codSubDim, algDefSetNode);
  TxtListTcl(ExtractSetField(depVal,SetOfText("CodDim","CodSubDim")))
};
//////////////////////////////////////////////////////////////////////////////
PutDescription("Dados una magnitud, una dimension y una subdimension y la 
definicion de un algebra devuelve el conjunto de nodos
que depende del valor del par dimension y una subdimension dado.",
GuiAlgGetValDepSubDim);
//////////////////////////////////////////////////////////////////////////////

//////////////////////////////////////////////////////////////////////////////
Text GuiAlgGetMagTclSet(Text codMag, Set algDef)
//////////////////////////////////////////////////////////////////////////////
{ 
  Set tclSet = AlgDefGetReg(codMag, algDef)->TclSet;
  Text If(IsEmpty(tclSet), "",  TxtListTcl(BinGroup("+",tclSet)))
};
//////////////////////////////////////////////////////////////////////////////
PutDescription("Retorna la informacion Tcl de una magnitud.",
GuiAlgGetMagTclSet);
//////////////////////////////////////////////////////////////////////////////

//////////////////////////////////////////////////////////////////////////////
Text GuiAlgGetNodeTclSet
(
  Text codMag, 
  Text codDim, 
  Text codSubDim, 
  Set algDef
)
//////////////////////////////////////////////////////////////////////////////
{
  Set algDefSetNode = AlgDefGetReg(codMag, algDef)->SetNode;
  Set node          = AlgDefGetNode(codDim, codSubDim, algDefSetNode);
  Text If(IsEmpty(node), "", {
    Set tclSet = node[1]->TclSet;
    If(IsEmpty(tclSet), "", TxtListTcl(BinGroup("+",tclSet)))
  })
};
//////////////////////////////////////////////////////////////////////////////
PutDescription("Retorna la informacion Tcl de un nodo.",
GuiAlgGetNodeTclSet);
//////////////////////////////////////////////////////////////////////////////

//////////////////////////////////////////////////////////////////////////////
// Standard selection
//////////////////////////////////////////////////////////////////////////////

//////////////////////////////////////////////////////////////////////////////
Real FunTrue(Set algSetNode)  
//////////////////////////////////////////////////////////////////////////////
{
  Real 1  // Devuelve true siempre
};

//////////////////////////////////////////////////////////////////////////////
Set FunEmpty(Set algSetNode) 
//////////////////////////////////////////////////////////////////////////////
{
  Copy(Empty)  // Devuelve empty siempre
};

//////////////////////////////////////////////////////////////////////////////
Text FunBlank(Set algSetNode) 
//////////////////////////////////////////////////////////////////////////////
{
  Text ""  // Devuelve "" siempre
};

