//////////////////////////////////////////////////////////////////////////////
// FILE:    _tabulate.tol 
// PURPOSE: Functions utility of TCL for tabulate matrixes, sets and series
//////////////////////////////////////////////////////////////////////////////

//////////////////////////////////////////////////////////////////////////////
Text TclTabulateSerie(Anything source, Set valArg)
//   Serie  source,   // Serie to tabulate
//   Text   title,    // Title for the table
//   Text   geometry) // MDI geometry specification
//////////////////////////////////////////////////////////////////////////////
{
  Text graVar = Grammar(source);
  If(Or(graVar == "Set", graVar == "Serie"),
  {
    // utiliza -title, -geometry
    Set defArg = SetOfSet (
      TclArgSt("-title", ""),
      TclArgSt("-geometry", "")
    );
    Set aplArg = TclMixArg( TclMixArg(SeedArg,defArg), valArg );
    
    Text cmd = "Tol_SerieTabulate source "+
         BBr(TclGAV("-title",aplArg))+" "+
         BBr(TclGAV("-geometry",aplArg));
  
    Set evalTcl = Tcl_Eval(cmd);
    Text If(Not(evalTcl[2]),
      WriteLn("<E>Error al tabular la serie. "+ evalTcl[1]+"</E>")
    );
    evalTcl["result"]
  }
  ,
  {
    TclError("El parámetro <source> ha de ser una Serie o un SetOfSerie")
  })
};
//////////////////////////////////////////////////////////////////////////////
PutDescription("
Función para tabular una Serie o conjunto de Series.
El valor de retorno es un Text.
  result : un Text con el path del gráfico visualizado o el error generado en
           caso de producirse
Únicamente funciona desde TolBase.
Parámetros:
  source   -> Serie o SetOfSeries a tabular .
  geometry -> Geometría de la ventana del gráfico. Ej: 814x692+10+10.
Ejemplo:
  Serie ser1 = Rand(-10, 10, Diario);
  Text  table01 = TclTabulateSerie(ser1, SetOfSet(
    TclArgSt(\"-title\",\"Tabulando unas Series\"),
    TclArgSt(\"-geometry\", \"814x692+15+15\")
  )); 
  Text  table01 = TclTabulateSerie(SetOfSerie(ser1), SetOfSet(
    TclArgSt(\"-title\",\"Tabulando unas Series\"),
    TclArgSt(\"-geometry\", \"814x692+15+15\")
  )); 
", TclTabulateSerie);
//////////////////////////////////////////////////////////////////////////////

//////////////////////////////////////////////////////////////////////////////
Text TclTabulateMatrix(Matrix source, Set valArg)
//   Matrix source,   // Matrix to graph
//   Text   title,    // Title for the table
//   Text   geometry) // MDI geometry specification
//////////////////////////////////////////////////////////////////////////////
{
  Set defArg = SetOfSet (
    TclArgSt("-title", ""),
    TclArgSt("-geometry", "")
  );
  Set aplArg = TclMixArg( TclMixArg(SeedArg,defArg), valArg );

  Set setSource = MatSet(source);
  Text result    = TclTabulateSet(setSource, SetOfSet(
    TclArgSt("-title",   TclGAV("-title",aplArg)),
    TclArgSt("-geometry",TclGAV("-geometry",aplArg))
  ))
};
//////////////////////////////////////////////////////////////////////////////
PutDescription("
Función para tabular una matriz.
El valor de retorno es un Text
  result : un Text con el path del gráfico visualizado o el error generado en
           caso de producirse
Únicamente funciona desde TolBase.
Parámetros:
  source   -> Matriz con los datos a graficar.
  geometry -> Geometría de la ventana del gráfico. Ej: 814x692+10+10.
Ejemplo:
  Matrix mat1    = Rand(100,   10, -10, 10);
  Set   tabla01 = TclTabulateMatrix(mat1, SetOfSet(
    TclArgSt(\"-title\",\"Tabulando una Matriz\"),
    TclArgSt(\"-geometry\", \"814x692+15+15\")
  ));
", TclTabulateMatrix);
//////////////////////////////////////////////////////////////////////////////

//////////////////////////////////////////////////////////////////////////////
Text TclTabulateSet(Set source, Set valArg)
//   Set    source,   // Set of sets
//   Text   title,    // Title for the table
//   Text   geometry) // MDI geometry specification
//////////////////////////////////////////////////////////////////////////////
{
  Set defArg = SetOfSet (
    TclArgSt("-title", ""),
    TclArgSt("-geometry", "")
  );
  Set aplArg = TclMixArg( TclMixArg(SeedArg,defArg), valArg );  

  Text cmd = "Tol_SetTabulate source "+
         BBr(TclGAV("-title",aplArg))+" "+
         BBr(TclGAV("-geometry",aplArg));

  Set evalTcl = Tcl_Eval(cmd);
  Text If(Not(evalTcl[2]),
    WriteLn("<E>Error al tabular la serie. "+ evalTcl[1]+"</E>")
  );
  evalTcl["result"]
};
//////////////////////////////////////////////////////////////////////////////
PutDescription("
Función para tabular un conjunto.
El valor de retorno es un Text
  result : un Text con el path del gráfico visualizado o el error generado en
           caso de producirse
Únicamente funciona desde TolBase.
Parámetros:
  source   -> Set con los datos a graficar.
  geometry -> Geometría de la ventana del gráfico. Ej: 814x692+10+10.
Ejemplo:
  Set set1 = 
    SetOfSet(
      SetOfText(\"a\",\"s\",\"d\",\"f\",\"g\",\"h\"),
      SetOfReal(  1,  2,  3,  4,  5,  6),
      SetOfReal( 12, 34, 35, 65, 76, 43),
      SetOfReal(  3,  7,  4,  2,  9,  6),
      SetOfReal(  2,  4,  5,  5,  6,  3));
  Set tabla01 = TclTabulateSet(set1, SetOfSet(
    TclArgSt(\"-title\",\"Tabulando un Set\"),
    TclArgSt(\"-geometry\", \"814x692+15+15\")
  ));
", TclTabulateSet);
//////////////////////////////////////////////////////////////////////////////


//////////////////////////////////////////////////////////////////////////////
Text TclTabulateTimeSet(Set source, Date dateIni, Date dateEnd, Set valArg)
//   TimeSet source,   // Set of TimeSet
//   Text    title,    // Title for the table
//   Text    geometry) // MDI geometry specification
//////////////////////////////////////////////////////////////////////////////
{
  Set defArg = SetOfSet (
    TclArgSt("-title", ""),
    TclArgSt("-geometry", "")
  );
  Set aplArg = TclMixArg( TclMixArg(SeedArg,defArg), valArg );  
  // build Set dates
  Date iniDate = If(IsUnknownDate(dateIni), DefFirst, dateIni);
  Date endDate = If(IsUnknownDate(dateEnd), DefLast,  dateEnd);
  Set sSr = EvalSet(source, Serie(TimeSet tms){
    SubSer(CalInd(tms, Diario), iniDate, endDate) 
  });
  Set sUnion = EvalSet(sSr, Serie(Serie s){
    DatChSerTmsSer(s, SetSum(sSr))
  });
  Set sFechasTabla = EvalSet(sUnion, Set(Serie ser){
    Set sfechas = Dates(Dating(ser), First(ser), Last(ser));
    Set sfechastabla = EvalSet(sfechas, Date(Date d){
      Date If(SerDat(ser, d) == 1, d, UnknownDate) 
    })
  });
  Set setFechasDef = Traspose(sFechasTabla);
  // table header
  Set header = EvalSet(source, Text (TimeSet tms) {
    Name(tms)
  });
  Set setFechas = SetOfSet(header) << setFechasDef;

  Text result    = TclTabulateSet(setFechas, SetOfSet(
    TclArgSt("-title",   TclGAV("-title",aplArg)),
    TclArgSt("-geometry",TclGAV("-geometry",aplArg))
  ))
};
//////////////////////////////////////////////////////////////////////////////
PutDescription("
Función para tabular un conjunto.
El valor de retorno es un Text
  result : un Text con el path del gráfico visualizado o el error generado en
           caso de producirse
Únicamente funciona desde TolBase.
Parámetros:
  source   -> Set con los datos a graficar.
  dateIni  -> Fecha de inicio. Si no se especifica, se toma DefFirst
  dateIni  -> Fecha de fin. Si no se especifica, se toma DefLast
  valArg:  -> Conjunto con los siguientes argumentos (estructura TclArgSt):
   -title    -> Título de la ventana del experto en calendarios.
   -geometry -> Geometría de la ventana. widthxheight+x+y . Ej: 814x692+10+10.
              Al especificar la geometría de la ventana hay que tener en
              cuenta, que se refiere a la geometría de la ventana, no a la del
              experto en calendarios.
Ejemplo:
  Set source = SetOfTimeSet(Easter, Mensual);
  Date dateIni = y2000m01d01;
  Date dateEnd = y2001m01d01;
  Text TclTabulateTimeSet(source, dateIni, dateEnd, SetOfSet(
    TclArgSt(\"-title\",\"Tabulando TimeSet\"),
    TclArgSt(\"-geometry\", \"100x100+1+1\")
  ));
", TclTabulateTimeSet);
//////////////////////////////////////////////////////////////////////////////
